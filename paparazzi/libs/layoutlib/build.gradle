import org.ajoberstar.grgit.Grgit

apply plugin: 'com.vanniktech.maven.publish'

version = libs.versions.layoutlib.get()

/**
 * Clone AOSP's prebuilts repo to get a prebuilt layoutlib jar. This big repo that takes a long time to clone!
 *
 * https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/refs/tags/studio-4.1.0/PREBUILT
 */
tasks.register('cloneLayoutlib') {
  it.outputs.dir(repoDir)
  it.inputs.property('sha', libs.versions.layoutlibPrebuiltSha)

  doFirst {
    // Gradle aggressively creates outputs directories, which interferes with the git clone check
    if (repoDir.exists() && !repoDir.list()) {
      repoDir.delete()
    }
    Grgit grgit
    if (!repoDir.exists()) {
      logger.warn('Cloning prebuilt layoutlib: this may take a few minutes...')
      grgit = Grgit.clone {
        dir = repoDir
        uri = "https://android.googlesource.com/platform/prebuilts/studio/layoutlib"
      }
      logger.warn('Cloned prebuilt layoutlib.')
    } else {
      logger.warn('Using existing prebuilt layoutlib clone.')
      grgit = Grgit.open {
        dir = repoDir
      }
    }
    grgit.withCloseable { repo ->
      repo.fetch()
      logger.warn("Checking out SHA ${libs.versions.layoutlibPrebuiltSha.get()}")
      repo.checkout {
        branch = libs.versions.layoutlibPrebuiltSha.get()
      }
    }
  }
}

/**
 * Publish layoutlib.jar to Maven Central with Paparazzi coordinates.
 */
publishing {
  publications {
    mavenNativeLibrary(MavenPublication) {
      artifact(tasks.
          named("cloneLayoutlib").
          map { new File(it.outputs.files.getSingleFile(), 'data/layoutlib.jar') }
      )
      artifact emptySourcesJar
      artifact emptyJavadocJar
    }
  }
}
