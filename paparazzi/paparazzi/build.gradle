import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.vanniktech.maven.publish'

sourceCompatibility = libs.versions.javaTarget.get()
targetCompatibility = libs.versions.javaTarget.get()

def artifactType = Attribute.of('artifactType', String)

configurations {
  unzip {
    attributes.attribute(artifactType, ArtifactTypeDefinition.DIRECTORY_TYPE)
  }
}

dependencies {
  registerTransform(org.gradle.api.internal.artifacts.transform.UnzipTransform) {
    from.attribute(artifactType, ArtifactTypeDefinition.JAR_TYPE)
    to.attribute(artifactType, ArtifactTypeDefinition.DIRECTORY_TYPE)
  }
}

dependencies {
  // Paparazzi is a Kotlin JVM module and thus cannot depend on Android library artifacts (AARs)
  // TODO: Use Gradle transforms to extract/rename classes.jar's from AARs
  api files('libs/compose-runtime-1.3.3.jar')
  api files('libs/compose-ui-1.3.3.jar')
  compileOnly files('libs/ui-geometry-1.3.3.jar')
  compileOnly files('libs/ui-graphics-1.3.3.jar')
  compileOnly files('libs/ui-text-1.3.3.jar')
  compileOnly files('libs/ui-unit-1.3.3.jar')
  compileOnly libs.androidx.lifecycleCommon
  compileOnly files('libs/lifecycle-runtime-2.5.0.jar')
  compileOnly files('libs/savedstate-1.2.0.jar')
  compileOnly files('libs/androidx-activity-1.5.0.jar')

  implementation libs.trove4j
  api libs.layoutlib.native.jdk11
  api libs.tools.common
  api libs.tools.layoutlib
  api libs.tools.ninepatch
  api libs.tools.sdkCommon
  api libs.kxml2
  api libs.junit
  api libs.androidx.annotations
  api libs.guava
  api libs.kotlinx.coroutines.core
  api libs.okio
  api platform(libs.kotlin.bom)
  implementation libs.moshi.core
  implementation libs.moshi.adapters
  kapt libs.moshi.kotlinCodegen
  implementation libs.jcodec.core
  implementation libs.jcodec.javase
  implementation projects.paparazziAgent

  def osName = System.getProperty("os.name").toLowerCase(Locale.US)
  if (osName.startsWith("mac")) {
    def osArch = System.getProperty("os.arch").toLowerCase(Locale.US)
    if (osArch.startsWith("x86")) {
      unzip libs.layoutlib.native.macOsX
    } else {
      unzip libs.layoutlib.native.macArm
    }
  } else if (osName.startsWith("windows")) {
    unzip libs.layoutlib.native.windows
  } else {
    unzip libs.layoutlib.native.linux
  }

  testImplementation libs.assertj

  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, libs.compose.compiler)
}

tasks.named("dokkaGfm").configure {
  outputDirectory = rootProject.file("../docs/1.x")

  dokkaSourceSets.named("main") {
    configureEach {
      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8
      perPackageOption {
        prefix = "app.cash.paparazzi.internal"
        suppress = true
      }
    }
  }
}

def generateTestConfig = tasks.register("generateTestConfig") {
  def resources = "$buildDir/intermediates/paparazzi/resources.txt"
  outputs.file(resources)

  doLast {
    File configFile = new File(resources)
    configFile.withWriter('utf-8') { writer ->
      writer.writeLine("app.cash.paparazzi")
      writer.writeLine(".")
      writer.writeLine("31")
      writer.writeLine("platforms/android-31/")
      writer.writeLine(".")
      writer.writeLine("app.cash.paparazzi")
    }
  }
}

tasks.withType(Test).configureEach {
  dependsOn(generateTestConfig)
  systemProperty(
    "paparazzi.test.resources",
    generateTestConfig.map { it.outputs.files.singleFile }.get().path
  )
  systemProperty(
    "paparazzi.build.dir",
    project.layout.buildDirectory.get().toString()
  )
  systemProperty(
    "paparazzi.platform.data.root",
    configurations.unzip.singleFile.absolutePath
  )
  // Uncomment to debug JNI issues in layoutlib
  // jvmArgs '-Xcheck:jni'
  testLogging {
    events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
    exceptionFormat 'FULL'
    showCauses true
    showExceptions true
    showStackTraces true
    showStandardStreams true
  }
}
