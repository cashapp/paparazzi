apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
    kotlin: '1.3.72',
    agp: '3.6.1',
    layoutlib: '3.4.0', // "should" be similar to versions.agp
    androidTools: '26.5.0', // agp + 23.0.0 = androidTools
    jcodec: '0.2.5',
    moshi: '1.9.2',
    bytebuddy: '1.10.11',
    releasedPaparazzi: '0.5.0-SNAPSHOT'
  ]

  ext.deps = [
    plugins: [
      android: "com.android.tools.build:gradle:${versions.agp}",
      kotlin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
    ],
    kotlin: [
      stdlib: [
        jdk8: "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
      ]
    ],
    kotlinx: [
      coroutines: [
        core: 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2',
      ],
    ],
    tools: [
      common: "com.android.tools:common:${versions.androidTools}",
      layoutlib: "com.android.tools.layoutlib:layoutlib-api:${versions.androidTools}",
      sdkcommon: "com.android.tools:sdk-common:${versions.androidTools}",
    ],
    kxml2: 'kxml2:kxml2:2.3.0',
    androidx: [
      annotations: 'androidx.annotation:annotation:1.1.0'
    ],
    layoutlib: "app.cash.paparazzi:layoutlib:${versions.layoutlib}",
    okio: 'com.squareup.okio:okio:2.4.0',
    moshi: [
      core: "com.squareup.moshi:moshi:${versions.moshi}",
      adapters: "com.squareup.moshi:moshi-adapters:${versions.moshi}",
      kotlinCodegen: "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}",
    ],
    jcodec: [
      core: "org.jcodec:jcodec:${versions.jcodec}",
      javase: "org.jcodec:jcodec-javase:${versions.jcodec}"
    ],
    bytebuddy: [
      core: "net.bytebuddy:byte-buddy:${versions.bytebuddy}",
      agent: "net.bytebuddy:byte-buddy-agent:${versions.bytebuddy}",
    ],
    guava: 'com.google.guava:guava:27.1-jre',
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:1.0',
    assertj: 'org.assertj:assertj-core:3.13.2'
  ]

  repositories {
    maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots/'
      content {
        includeGroup 'app.cash.paparazzi'
      }
    }
    google()
    mavenCentral()
    jcenter()
    //mavenLocal()
  }

  dependencies {
    classpath deps.plugins.kotlin
    classpath deps.plugins.android
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.18'
    //classpath "app.cash.paparazzi:paparazzi-gradle-plugin:${versions.releasedPaparazzi}"
  }
}

subprojects {
  repositories {
    maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots/'
      content {
        includeGroup 'app.cash.paparazzi'
      }
    }
    google()
    mavenCentral()
    jcenter()
    maven {
      url 'https://dl.bintray.com/kotlin/dokka'
    }
    //mavenLocal()
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
