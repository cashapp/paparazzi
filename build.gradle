apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
    kotlin: '1.3.50',
    agp: '3.5.0',
    layoutlib: '3.4.0', // "should" be similar to versions.agp
    androidTools: '26.5.0', // agp + 23.0.0 = androidTools
    jcodec: '0.2.5',
    moshi: '1.8.0'
  ]

  ext.deps = [
    plugins: [
      android: "com.android.tools.build:gradle:${versions.agp}",
      kotlin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
    ],
    kotlin: [
      stdlib: [
        jdk8: "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
      ]
    ],
    kotlinx: [
      coroutines: [
        core: 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2',
      ],
    ],
    tools: [
      common: "com.android.tools:common:${versions.androidTools}",
      layoutlib: "com.android.tools.layoutlib:layoutlib-api:${versions.androidTools}",
      sdkcommon: "com.android.tools:sdk-common:${versions.androidTools}",
    ],
    kxml2: 'kxml2:kxml2:2.3.0',
    androidx: [
      annotations: 'androidx.annotation:annotation:1.1.0'
    ],
    layoutlib: "app.cash.paparazzi:layoutlib:${versions.layoutlib}",
    okio: 'com.squareup.okio:okio:2.4.0',
    moshi: [
      core: "com.squareup.moshi:moshi:${versions.moshi}",
      adapters: "com.squareup.moshi:moshi-adapters:${versions.moshi}"
    ],
    jcodec: [
      core: "org.jcodec:jcodec:${versions.jcodec}",
      javase: "org.jcodec:jcodec-javase:${versions.jcodec}"
    ],
    guava: 'com.google.guava:guava:27.1-jre',
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:1.0',
    assertj: 'org.assertj:assertj-core:3.13.2'
  ]

  repositories {
    google()
    mavenCentral()
    jcenter()
    mavenLocal()
  }

  dependencies {
    classpath deps.plugins.kotlin
    classpath deps.plugins.android
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.25.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.18'
    classpath 'app.cash.paparazzi:paparazzi-gradle-plugin:1.0.0-square08'
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven {
      url 'https://dl.bintray.com/kotlin/dokka'
    }
    mavenLocal()
  }

  group = GROUP
  version = VERSION_NAME
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}