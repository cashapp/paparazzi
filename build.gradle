apply plugin: 'com.github.ben-manes.versions'

buildscript {
  // maps to this commit: https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/b169460c8473a1e87aafa01da881556682fa4f86
  // which runs on JDK 11
  ext.layoutLibPrebuiltSha = 'b169460'

  ext.versions = [
    kotlin: '1.5.10',
    agp: '4.2.2',
    layoutlib: "4.0.0-alpha02-$layoutLibPrebuiltSha", // "should" be similar to versions.agp
    androidTools: '27.2.2', // agp + 23.0.0 = androidTools
    jcodec: '0.2.5',
    moshi: '1.12.0',
    bytebuddy: '1.11.15',
    releasedPaparazzi: '0.7.1'
  ]

  ext.deps = [
    plugins: [
      android: "com.android.tools.build:gradle:${versions.agp}",
      kotlin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
    ],
    kotlinx: [
      coroutines: [
        core: 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1',
      ],
    ],
    tools: [
      common: "com.android.tools:common:${versions.androidTools}",
      layoutlib: "com.android.tools.layoutlib:layoutlib-api:26.6.0",
      sdkcommon: "com.android.tools:sdk-common:26.6.4",
    ],
    kxml2: 'kxml2:kxml2:2.3.0',
    androidx: [
      annotations: 'androidx.annotation:annotation:1.2.0'
    ],
    layoutlib: "app.cash.paparazzi:layoutlib-jdk11:${versions.layoutlib}",
    okio: 'com.squareup.okio:okio:2.10.0',
    moshi: [
      core: "com.squareup.moshi:moshi:${versions.moshi}",
      adapters: "com.squareup.moshi:moshi-adapters:${versions.moshi}",
      kotlinCodegen: "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}",
    ],
    jcodec: [
      core: "org.jcodec:jcodec:${versions.jcodec}",
      javase: "org.jcodec:jcodec-javase:${versions.jcodec}"
    ],
    bytebuddy: [
      core: "net.bytebuddy:byte-buddy:${versions.bytebuddy}",
      agent: "net.bytebuddy:byte-buddy-agent:${versions.bytebuddy}",
    ],
    guava: 'com.google.guava:guava:30.1.1-jre',
    junit: 'junit:junit:4.13.2',
    truth: 'com.google.truth:truth:1.1.3',
    assertj: 'org.assertj:assertj-core:3.20.2',
    testparameterinjector: 'com.google.testparameterinjector:test-parameter-injector:1.4',
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    //mavenLocal()
  }

  dependencies {
    classpath deps.plugins.kotlin
    classpath deps.plugins.android
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.17.0'
    classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.5.0'
    classpath "app.cash.paparazzi:paparazzi-gradle-plugin:${versions.releasedPaparazzi}"
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    //mavenLocal()
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(Test).configureEach {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    publishing {
      repositories {
        maven {
          name = "projectLocalMaven"
          url = "${rootProject.buildDir}/localMaven"
        }
      }
    }
  }

  tasks.register('emptySourcesJar', Jar) {
    // TODO: fetch sources from the corresponding AOSP repos.
    archiveClassifier = 'sources'
  }

  tasks.register('emptyJavadocJar', Jar) {
    archiveClassifier = 'javadoc'
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
