plugins {
  id 'com.gradle.develocity' version '3.17.5'
  id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.1'
}

develocity {
  def isCI = System.getenv('CI') != null // adjust to your CI provider
  server = 'https://ge.solutions-team.gradle.com/' // adjust to your Develocity server
  allowUntrustedServer = false // ensure a trusted certificate is configured

  buildScan {
    uploadInBackground = !isCI
  }
}

//develocity {
//  buildScan {
//    termsOfUseUrl = 'https://gradle.com/help/legal-terms-of-use'
//    if (System.getenv('CI') == 'true') {
//      termsOfUseAgree = 'yes'
//      tag 'CI'
//    } else {
//      publishing.onlyIf { false }
//    }
//  }
//}

rootProject.name = 'paparazzi-root'

include ':paparazzi'
include ':paparazzi-annotations'
include ':paparazzi-preview-processor'
include ':paparazzi-gradle-plugin'

include ':sample'

enableFeaturePreview('TYPESAFE_PROJECT_ACCESSORS')

includeBuild('build-logic') {
  dependencySubstitution {
    substitute module('app.cash.paparazzi:paparazzi-gradle-plugin') using project(':paparazzi-gradle-plugin')
    substitute module('app.cash.paparazzi:aar2jar') using project(':aar2jar')
  }
}

def isIdea = !(System.getProperty("idea.version") ?: "").isEmpty()
if (isIdea) {
  includeTestProjectsIntoIDE()
}

private void includeTestProjectsIntoIDE() {
  // Some of the test projects are intentionally supposed to fail. Don't import these into the main project.
  def brokenProjects = [
    "invalid-application-plugin", // intentionally does not import
    "missing-library-plugin", // intentionally does not import
    "missing-supported-plugins", // intentionally does not import
    "multiplatform-plugin-without-android", // intentionally does not import
  ]

  new File(rootDir, "paparazzi-gradle-plugin/src/test/projects").eachDir {
    def dirName = it.name
    if (!dirName.startsWith(".") && !brokenProjects.contains(dirName)) {
      include ":test-projects:$dirName"
      project(":test-projects:$dirName").projectDir = new File(rootDir, "paparazzi-gradle-plugin/src/test/projects/$dirName")
    }
  }
}
