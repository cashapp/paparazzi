import org.ajoberstar.grgit.Grgit

apply plugin: 'com.vanniktech.maven.publish'

/**
 * Clone AOSP's prebuilts repo to get a prebuilt layoutlib jar. This big repo that takes a long time to clone!
 *
 * https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/refs/tags/studio-4.1.0/PREBUILT
 */
tasks.register('cloneLayoutlib') {
  it.outputs.dir(repoDir)
  it.inputs.property('sha', libs.versions.layoutlibPrebuiltSha)

  doFirst {
    // Gradle aggressively creates outputs directories, which interferes with the git clone check
    if (repoDir.exists() && !repoDir.list()) {
      repoDir.delete()
    }
    Grgit grgit
    String tag = "refs/tags/studio-${libs.versions.layoutlibPrebuiltSha.get()}"
    if (!repoDir.exists()) {
      logger.warn('Cloning prebuilt layoutlib: this may take a few minutes...')
      grgit = Grgit.clone {
        dir = repoDir
        uri = "https://android.googlesource.com/platform/prebuilts/studio/layoutlib"
        depth = 1
        branches = [ tag ]
        all = false
        refToCheckout = tag
      }
      logger.warn('Cloned prebuilt layoutlib.')
    } else {
      logger.warn('Using existing prebuilt layoutlib clone.')
      grgit = Grgit.open {
        dir = repoDir
      }
    }
    grgit.withCloseable { repo ->
      logger.warn("Fetching tag ${tag}")
      repo.fetch {
        refSpecs = [ tag ]
        depth = 1
      }
      logger.warn("Checking out tag ${tag}")
      repo.checkout {
        branch = tag
      }
    }
  }
}

/**
 * Publish layoutlib.jar to Maven Central with Paparazzi coordinates.
 */
mavenPublishing {
  coordinates(property("GROUP"), property("POM_ARTIFACT_ID"), libs.versions.layoutlib.get())
}

publishing {
  publications {
    mavenNativeLibrary(MavenPublication) {
      artifact(tasks.
          named("cloneLayoutlib").
          map { new File(it.outputs.files.getSingleFile(), 'data/layoutlib.jar') }
      )
      artifact emptySourcesJar
      artifact emptyJavadocJar
    }
  }
}
