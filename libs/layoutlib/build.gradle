apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'com.vanniktech.maven.publish'

version = versions.layoutlib

/**
 * Clone AOSP's prebuilts repo to get a prebuilt layoutlib jar. This big repo that takes a long time to clone!
 *
 * https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/refs/tags/studio-4.1.0/PREBUILT
 */
tasks.register('cloneLayoutlib') {
  it.outputs.dir(repoDir)
  it.inputs.property('sha', layoutLibPrebuiltSha)

  doFirst {
    // Gradle aggressively creates outputs directories, which interferes with the git clone check
    if (repoDir.exists() && !repoDir.list()) {
      repoDir.delete()
    }
    if (!repoDir.exists()) {
      logger.warn('Cloning prebuilt layoutlib: this make take a few minutes...')
      grgit.clone {
        dir = repoDir
        uri = "https://android.googlesource.com/platform/prebuilts/studio/layoutlib"
      }
      logger.warn('Cloned prebuilt layoutlib.')
    }

    def repo = grgit.open {
      dir = repoDir
    }

    logger.warn("Checking out SHA $layoutLibPrebuiltSha")
    repo.checkout {
      branch = layoutLibPrebuiltSha
    }
  }
}

/**
 * Publish layoutlib.jar to Maven Central with Paparazzi coordinates.
 */
publishing {
  publications {
    mavenNativeLibrary(MavenPublication) {
      artifact(tasks.
          named("cloneLayoutlib").
          map { new File(it.outputs.files.getSingleFile(), 'data/layoutlib_native.jar') }
      )
      artifact emptySourcesJar
      artifact emptyJavadocJar
    }
  }
}
