apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 28
  defaultConfig {
    minSdkVersion 25
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  testImplementation project(':paparazzi')
}

android.libraryVariants.all { variant ->
  def variantSlug = variant.name.capitalize()

  def verifyPaparazzi = tasks.register("verifyPaparazzi${variantSlug}Resources") {
    // TODO: variant-aware file path
    def outputFile = "${project.buildDir}/intermediates/paparazzi/resources.txt"
    outputs.file(outputFile)
    dependsOn variant.mergeResourcesProvider

    doLast {
      def out = project.file(outputFile)
      out.delete()
      out << variant.mergeResourcesProvider.get().outputDir << '\n'
    }
  }

  tasks.named("test${variant.unitTestVariant.name.capitalize()}").configure {
    it.dependsOn(verifyPaparazzi)
  }
}
