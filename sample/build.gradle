/** Apply following to Test CustomButton in MainActivity **/
apply plugin: 'com.android.application'

/** Apply following to Test Paparazzi and comment out above **/
//apply plugin: 'com.android.library'

apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'app.cash.paparazzi'
apply plugin: 'jacoco'

android {
  namespace 'app.cash.paparazzi.sample'

  compileSdk libs.versions.compileSdk.get() as int
  defaultConfig {
    minSdk libs.versions.minSdk.get() as int
    resValue("string", "generated_string_name", "Generated Test String")
  }
  compileOptions {
    sourceCompatibility = libs.versions.javaTarget.get()
    targetCompatibility = libs.versions.javaTarget.get()
  }
  buildFeatures {
    compose true
    viewBinding true
  }
}

dependencies {
  implementation libs.composeUi.material
  implementation libs.composeUi.material.icons
  implementation libs.composeUi.uiTooling
  implementation "androidx.activity:activity-compose:1.9.3"
  implementation 'com.google.android.material:material:1.12.0'
  // Remove if https://github.com/airbnb/lottie-android/pull/2507 lands
  implementation libs.androidx.appcompat
  // deliberately not in versions.toml as a one-off
  implementation 'com.airbnb.android:lottie:6.6.0'

  // TODO: wire via plugin
  implementation projects.paparazziAnnotations
  lintChecks projects.paparazziPreviewLints

  testImplementation libs.testParameterInjector
}

// https://github.com/diffplug/spotless/issues/1572
tasks.withType(com.diffplug.gradle.spotless.SpotlessTask).configureEach {
  mustRunAfter(tasks.withType(Test))
}
